title: TestCapsule
---
/// Whether Capsley like you or not. This starts true, but may change.
<<declare $capsley_likes_you = true as bool>>
/// The player's name. The player chooses this. It starts empty.
<<declare $player_name = "" as string>>

Capsley: Hello, I am Mr Capsley. #line:04df4da 
Capsley: Who are you then? #line:00ef629 

-> I'm Capsule, but my friends call me "Tic Tac". No idea why... #line:0132556 
    <<set $player_name to "Tic Tac">>
-> The name's Triquandle. #line:034dd0d 
    <<set $player_name to "Triquandle">>
-> Pyramid. Why - who wants to know? #line:003851f 
    <<set $player_name to "Pyramid">>
    <<set $capsley_likes_you to false>>

<<if $capsley_likes_you>>
    Capsley: Nice to meet you {$player_name}! #line:0a96a9d 
<<else>>
    Capsley: No need to be so rude... #line:0f8ef9c 
    Capsley: Maybe you should be called Grumpy {$player_name}. #line:070aab9 
<<endif>>
===
